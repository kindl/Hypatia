module Obj

import Prelude((&), notEq, toNumber, file, getn, error,
    Numeral, Text, compose, arrayFoldMap, arrayMap)
import Geometry(Vector, Vec4)
import Array(filter, inspect, split, map, foldMap, lines, words, Array)


type VertexIndex
    = VI Numeral
    | VTI Numeral Numeral
    | VNI Numeral Numeral
    | VTNI Numeral Numeral Numeral

type ObjElement
    = Vertex (Vector Numeral)
    | TextureCoordinate (Vector Numeral)
    | Normal (Vector Numeral)
    | Face (Array VertexIndex)
    | Comment (Array Text)

toFace (Face f) = [f]
toFace _ = []

toVertex (Vertex v) = [v]
toVertex _ = []

toNormal (Normal n) = [n]
toNormal _ = []

getFaces = foldMap toFace
getVertices = foldMap toVertex
getNormals = foldMap toNormal

# words splits a line at every space
# However "v -1.0 -1.0 -1.0"
# splits into ["v", "-1.0", "-1.0", "-1.0", ""]
# because of an invisible \r at the end. Ouch!
# Therefore the "" is filtered out
parseLine s = matchLine (filter (notEq "") (words s))

load filePath = map parseLine (lines (file filePath))

matchLine es =
    case getn 1 es of
        "v" -> Vertex (readVector es)
        "vn" -> Normal (readVector es)
        "vt" -> TextureCoordinate (readVector es)
        "f" -> Face (map readVI (filter (notEq "f") es))
        v -> Comment [v]

readVector [_, v1, v2, v3, v4] =
    Vec4 (toNumber v1) (toNumber v2) (toNumber v3) (toNumber v4)
readVector [_, v1, v2, v3] =
    Vec4 (toNumber v1) (toNumber v2) (toNumber v3) 1
readVector [_, v1, v2] =
    Vec4 (toNumber v1) (toNumber v2) 0 1
readVector [_, v1] =
    Vec4 (toNumber v1) 0 0 1

readVI s = case split "/" s of
    [vi] -> VI (toNumber vi)
    [vi, vt] -> VTI (toNumber vi) (toNumber vt)
    [vi, "", vn] -> VNI (toNumber vi) (toNumber vn)
    [vi, vt, vn] -> VTNI (toNumber vi) (toNumber vt) (toNumber vn)
